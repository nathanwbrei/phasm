#----------------------------------------------------------------------------
# Setup the project
#
cmake_minimum_required(VERSION 3.16...3.21)
project(B4d)

#----------------------------------------------------------------------------
# Find Geant4 package, activating all available UI and Vis drivers by default
# You can set WITH_GEANT4_UIVIS to OFF via the command line or ccmake/cmake-gui
# to build a batch mode only executable
#
option(WITH_GEANT4_UIVIS "Build example with Geant4 UI and Vis drivers" ON)
if(WITH_GEANT4_UIVIS)
  find_package(Geant4 REQUIRED ui_all vis_all)
else()
  find_package(Geant4 REQUIRED)
endif()

#----------------------------------------------------------------------------
# Setup Geant4 include directories and compile definitions
# Setup include directory for this project
#
include(${Geant4_USE_FILE})
include_directories(${PROJECT_SOURCE_DIR}/include)

#----------------------------------------------------------------------------
# Locate sources and headers for this project
# NB: headers are included so they will show up in IDEs
#
file(GLOB sources ${PROJECT_SOURCE_DIR}/src/*.cc)
file(GLOB headers ${PROJECT_SOURCE_DIR}/include/*.hh)

#----------------------------------------------------------------------------
# Add the executable, and link it to the Geant4 libraries
#
add_executable(phasm-example-calorimeter-B4d exampleB4d.cc ${sources} ${headers})
target_link_libraries(phasm-example-calorimeter-B4d ${Geant4_LIBRARIES} phasm-surrogate)
set_target_properties(phasm-example-calorimeter-B4d PROPERTIES INSTALL_RPATH_USE_LINK_PATH True)

#----------------------------------------------------------------------------
# Copy all scripts to the build directory, i.e. the directory in which we
# build B4d. This is so that we can run the executable directly because it
# relies on these scripts being in the current working directory.
#
set(EXAMPLEB4D_SCRIPTS
  gui.mac
  init_vis.mac
  plotHisto.C
  plotNtuple.C
  run_b4d.mac
  vis.mac
  )

foreach(_script ${EXAMPLEB4D_SCRIPTS})
  configure_file(
    ${PROJECT_SOURCE_DIR}/${_script}
    ${PROJECT_BINARY_DIR}/${_script}
    COPYONLY
    )
endforeach()

#----------------------------------------------------------------------------
# Install the executable to 'share/examples/calorimeter' directory under CMAKE_INSTALL_PREFIX
#
install(TARGETS phasm-example-calorimeter-B4d DESTINATION share/examples/calorimeter)
install(FILES run_b4d.mac vis.mac init_vis.mac gui.mac plotHisto.C plotNtuple.C DESTINATION share/examples/calorimeter)
